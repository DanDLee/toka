#! /usr/bin/toka
#! ---------------------------------------------------------------
#! RFC862 Compliant Echo Server
#!
#! Copyright (c)2007 by Charles Childers
#! This code may be used under the terms of the MIT/X11 License.
#! ---------------------------------------------------------------

needs sockets
3 import memset     #! void *memset(void *s, int c, size_t n);


4096 chars is-array buffer
[ buffer 0 4096 chars memset reset ] is reset


#! ---------------------------------------------------------------
#! new-connection
#! Setup a new connection. This involves bindings port 7 to 
#! localhost, and waiting for a client to connect to it. The 
#! connection and socket variables are set by this code.
#!
#! end-connection
#! Closes the current connection. This allows for new connections
#! to be made.
#! ---------------------------------------------------------------
variable| socket connection |

7 pBind socket !
[ socket @ pAccept connection ! ] is new-connection
[ connection @ pClose ] is end-connection


#! ---------------------------------------------------------------
#! send
#! Send a string to the currently connected client
#! ---------------------------------------------------------------
[ >r connection @ r> count char- pWrite ] is send


#! =============================================================== MAIN LOOP
[
  new-connection
  [
    connection @ buffer 4096 chars pRead
    buffer send >r
    reset
    r> -1 <>
  ] whileTrue
  TRUE
] keep whileTrue
bye
