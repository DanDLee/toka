#! /usr/bin/toka
#! --------------------------------------------------------
#! This is a set of benchmarks for Toka. It requires the
#! following libraries to be installed:
#!
#!  - time
#!  - shell
#!
#! The basic idea is to take common operations and run them
#! insane numbers of times. In many cases, this will mean
#! cycling them in a loop 100 million times or more.
#!
#! File operations get off a bit easier; we only create and
#! read in files about 100 times, mostly since I/O is known
#! to be slower than other operations.
#!
#! --------------------------------------------------------

needs time
needs shell
needs values
needs bsort
needs math

[ lnparse type cr reset ] is t:

{
  time_t start
  time_t end

  [ start time drop
    invoke
    end time drop
    end @ start @ - . ." seconds elapsed\n"
  ]
} is :time

t: Stage 1: Empty Quotes
t: ----------------------------------
t: 100,000,000 iterations
[ 100000000 [ ] iterate ] :time
cr


t: Stage 2: "1 dup swap drop drop"
t: ----------------------------------
t: 100,000,000 iterations
[ 100000000 [ 1 dup swap drop drop ] iterate ] :time
cr


t: Stage 3: foo @ foo !
t: ----------------------------------
variable foo
t: 100,000,000 iterations
[ 100000000 [ foo @ foo ! ] iterate ] :time
cr


t: Stage 4: Math Operations
t: ----------------------------------
t: a) 1 2 + (100,000,000 iterations)
[ 100000000 [ 1 2 + drop ] iterate ] :time
t: b) 2 1 - (100,000,000 iterations)
[ 100000000 [ 2 1 - drop ] iterate ] :time
t: c) 4 3 * (100,000,000 iterations)
[ 100000000 [ 4 3 * drop ] iterate ] :time
t: d) 5 2 /mod (100,000,000 iterations)
[ 100000000 [ 5 2 /mod 2drop ] iterate ] :time
t: e) 4 2 / (100,000,000 iterations)
[ 100000000 [ 4 2 / drop ] iterate ] :time
t: f) 5 2 mod (100,000,000 iterations)
[ 100000000 [ 5 2 mod drop ] iterate ] :time
cr


t: Stage 5: bsort (6000 element array)
t: ----------------------------------
6000 cells is-array foo
6000 [ random i foo put-element ] iterate
[ foo 6000 bsort ] :time
cr


t: Stage 6: File Operations
t: ----------------------------------
t: a) Creating a 25MiB file (100 iterations)
variable| fid buffer |
1024 1024 * 25 * malloc buffer !
[ 100
  [ " /tmp/big.toka" "W" file.open fid !
    fid @ buffer @ 1024 1024 * 25 * file.write drop 
    fid @ file.close 
  ] iterate
] :time
t: b) Slurping into a buffer (100 iterations)
[ 100 [ " /tmp/big.toka" file.slurp drop ] iterate ] :time
t: c) Cleaning up...
#! [ " /tim/big.toka" remove gc ] :time
cr


t: ----------------------------------
t: All done!
t: ----------------------------------
bye
