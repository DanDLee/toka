[ 10 parse drop ] is #!

#! ---------------------------------------
#! Constants for various ASCII values
#! ---------------------------------------
[ 32 ] is SPACE
[ 10 ] is CR
[ 13 ] is LF
[ 27 ] is ESC
[  9 ] is TAB


#! ---------------------------------------
#! Parsing extensions
#! ---------------------------------------
[ SPACE parse ] is wsparse
[ CR parse ] is lnparse


#! ---------------------------------------
#! Conditionals
#! ---------------------------------------
[  0 ] is FALSE
[ -1 ] is TRUE
[ [ ] t/f ] is true?
[ [ ] swap t/f ] is false?


#! ---------------------------------------
#! Obtaining Character
#! ---------------------------------------
[ 255 and ] is >char
[ SPACE parse @ >char
  compiler @ [ # ] true?
] is-super char:


#! ---------------------------------------
#! Strings
#! ---------------------------------------
[ char: " parse
  compiler @
  [ count dup malloc dup # swap copy ]
  true?
] is-super "


#! ---------------------------------------
#! Output
#! ---------------------------------------
[ CR LF emit emit ] is cr
[ SPACE emit ] is space
[   TAB emit ] is tab
[ lnparse type cr ] is t:
[ " \^[2J" type
  " \^[1;1H" type
] is clear


#! ---------------------------------------
#! Colors
#! ---------------------------------------
[ " \^[0m"  type ] is normal
[ " \^[1m"  type ] is bold
[ " \^[30m" type ] is black
[ " \^[31m" type ] is red
[ " \^[32m" type ] is green
[ " \^[33m" type ] is yellow
[ " \^[34m" type ] is blue
[ " \^[35m" type ] is magenta
[ " \^[36m" type ] is cyan
[ " \^[37m" type ] is white
[ " \^[40m" type ] is onBlack
[ " \^[41m" type ] is onRed
[ " \^[42m" type ] is onGreen
[ " \^[43m" type ] is onYellow
[ " \^[44m" type ] is onBlue
[ " \^[45m" type ] is onMagenta
[ " \^[46m" type ] is onCyan
[ " \^[47m" type ] is onWhite


#! ---------------------------------------
#! Stack manipulation
#! ---------------------------------------
[ swap drop ] is nip
[ >r swap r> swap ] is rot
[ swap >r swap r> ] is -rot
[ >r dup r> swap ] is over
[ dup -rot ] is tuck
[ over over ] is 2dup
[ drop drop ] is 2drop
[ depth [ drop ] iterate ] is reset
[ r> dup >r ] is r@


#! ---------------------------------------
#! Math
#! ---------------------------------------
[ 1 + ] is 1+
[ 1 - ] is 1-
[ -1 * ] is negate
[ /mod nip ] is /
[ /mod drop ] is mod
[ -1 xor ] is not
[ swap >r / r> * ] is */


#! ---------------------------------------
#! Memory and Variables
#! ---------------------------------------
[ char-size * ] is chars
[ char-size + ] is char+
[ char-size - ] is char-
[ cell-size * ] is cells
[ cell-size + ] is cell+
[ cell-size - ] is cell-
[ dup @ rot + swap ! ] is +!
[ dup @ rot - swap ! ] is -!
[ TRUE  swap ! ] is on
[ FALSE swap ! ] is off
[ dup @ not swap ! ] is toggle
[ cell-size malloc 
  parser @ [ swap ] false? is-data 
] is variable
[ parser @ >r
  parser off
  [ SPACE parse
    dup c@ char: | <>
    [ variable TRUE ] [ drop FALSE ] t/f
  ] whileTrue
  r> parser !
] is variable|


#! ---------------------------------------
#! Numeric bases
#! ---------------------------------------
[ 16 base ! ] is hex
[ 10 base ! ] is decimal
[  2 base ! ] is binary
[  8 base ! ] is octal


#! ---------------------------------------
#! File I/O
#! ---------------------------------------
[ 1 ] is "R"
[ 2 ] is "R+"
[ 3 ] is "W"
[ 4 ] is "W+"
[ 5 ] is "A"
[ 6 ] is "A+"
[ 1 ] is START
[ 2 ] is CURRENT
[ 3 ] is END

[ "R" file.open dup dup
  file.size 1 + dup malloc dup >r swap
  file.read drop
  file.close r>
] is file.slurp


#! ---------------------------------------
#! Arrays
#! ---------------------------------------
[ parser @
  [ malloc ] [ >r malloc r> ] t/f is-data 
] is is-array
[ swap cell-size * + @ ] is get-element
[ swap cell-size * + ! ] is put-element
[ swap char-size * + c@ ] is get-char-element
[ swap char-size * + c! ] is put-char-element


#! ---------------------------------------
#! Scope
#! ---------------------------------------
4 cells is-array <list>

[ last @ <list> @ 1+ <list> put-element
  1 <list> +!
] is {
[ 1 <list> -!
  <list> @ 1+ <list> get-element last !
] is }
